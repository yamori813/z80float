	TITLE	DSIN (V02/L01)
;
	GLOBAL	DSIN
	EXTERN	DFIX
	EXTERN	DADD
	EXTERN	DSUB
	EXTERN	DCMP
	EXTERN	DMLT
	EXTERN	DDIV
	EXTERN	DMOV

; Delete not use macro

DSIN	EQU	$
	LD	(DSTADR),DE
	LD	DE,X
	CALL	DMOV
	LD	HL,X+7
	LD	A,(HL)
	LD	C,A
	AND	80H
	LD	(SGNX),A
	LD	A,C
	AND	7FH
	LD	(HL),A
	LD	HL,X
	LD	DE,X1
	CALL	DMOV
	LD	HL,PAI20
	LD	DE,X1
	CALL	DDIV
	LD	HL,X1
	LD	D,H
	LD	E,L
	CALL	DFIX
	LD	HL,PAI20
	LD	DE,X1
	CALL	DMLT
	LD	HL,X1
	LD	DE,X
	CALL	DSUB
	LD	HL,PAI32
	LD	DE,X
	CALL	DCMP
	JP	P,SIN1
	LD	HL,PAI10
	LD	DE,X
	CALL	DCMP
	JP	P,SIN2
	LD	HL,PAI02
	LD	DE,X
	CALL	DCMP
	JP	P,SIN3
SINSUM	EQU	$
	LD	HL,X
	LD	DE,X1
	CALL	DMOV
	LD	HL,X
	LD	DE,X
	CALL	DMLT
	LD	HL,X+7
	SET	7,(HL)
	LD	HL,X2
	LD	B,8
	XOR	A
SINCLR	EQU	$
	LD	(HL),A
	INC	HL
	DJNZ	SINCLR
	LD	HL,K1
	LD	DE,X3
	CALL	DMOV
	LD	HL,K1
	LD	DE,X4
	CALL	DMOV
	LD	B,11
SINLP	EQU	$
	PUSH	BC
	LD	HL,X1
	LD	DE,X5
	CALL	DMOV
	LD	HL,X3
	LD	DE,X5
	CALL	DDIV
	LD	HL,X5
	LD	DE,X2
	CALL	DADD
	POP	BC
	DJNZ	SINLP1
	JR	SINLP2
SINLP1	EQU	$
	PUSH	BC
	LD	HL,K1
	LD	DE,X4
	CALL	DADD
	LD	HL,X4
	LD	DE,X3
	CALL	DMLT
	LD	HL,K1
	LD	DE,X4
	CALL	DADD
	LD	HL,X4
	LD	DE,X3
	CALL	DMLT
	LD	HL,X
	LD	DE,X1
	CALL	DMLT
	POP	BC
	JR	SINLP
SINLP2	EQU	$
	LD	A,(SGNX)
	LD	C,A
	LD	HL,X2+7
	LD	A,(HL)
	XOR	C
	LD	(HL),A
	LD	HL,X2
	LD	DE,(DSTADR)
	CALL	DMOV
	XOR	A
	RET
SIN1	EQU	$
	LD	HL,PAI20
	LD	DE,X1
	CALL	DMOV
	LD	HL,X
	LD	DE,X1
	CALL	DSUB
	LD	HL,X1
	LD	DE,X
	CALL	DMOV
	LD	HL,SGNX
	LD	C,(HL)
	LD	A,80H
	XOR	C
	LD	(HL),A
	JP	SINSUM
SIN2	EQU	$
	LD	HL,PAI10
	LD	DE,X
	CALL	DSUB
	LD	HL,SGNX
	LD	C,(HL)
	LD	A,80H
	XOR	C
	LD	(HL),A
	JP	SINSUM
SIN3	EQU	$
	LD	HL,PAI10
	LD	DE,X1
	CALL	DMOV
	LD	HL,X
	LD	DE,X1
	CALL	DSUB
	LD	HL,X1
	LD	DE,X
	CALL	DMOV
	JP	SINSUM
;
DSTADR:	DEFW	0
SGNX:	DEFB	0
X:	DEFB	0,0,0,0,0,0,0,0
X1:	DEFB	0,0,0,0,0,0,0,0
X2:	DEFB	0,0,0,0,0,0,0,0
X3:	DEFB	0,0,0,0,0,0,0,0
X4:	DEFB	0,0,0,0,0,0,0,0
X5:	DEFB	0,0,0,0,0,0,0,0
PAI20:	DEFB	5FH,0B4H,10H,51H
	DEFB	0EDH,87H,64H,41H
PAI10:	DEFB	2FH,5AH,88H,0A8H
	DEFB	0F6H,43H,32H,41H
PAI02:	DEFB	1AH,2DH,44H,54H
	DEFB	0FBH,21H,19H,41H
PAI32:	DEFB	49H,87H,0CCH,0FCH
	DEFB	0F1H,65H,4BH,41H
K1:	DEFB	0,0,0,0
	DEFB	0,0,10H,41H
	END
