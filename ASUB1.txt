	TITLE	ASUB1 (V02/L01)
;
	GLOBAL	.ADD
	GLOBAL	.SUB
	GLOBAL	.CMP
	GLOBAL	.NEG
	GLOBAL	.SL1
	GLOBAL	.SL4
	GLOBAL	.SR1
	GLOBAL	.SR4
	GLOBAL	.CLR
	GLOBAL	.ZRC
;
.ADD:	XOR	A
ADDLP:	LD	A,(DE)
	ADC	A,(HL)
	LD	(DE),A
	INC	HL
	INC	DE
	DJNZ	ADDLP
	RET
;
.SUB:	XOR	A
SUBLP:	LD	A,(DE)
	SBC	A,(HL)
	LD	(DE),A
	INC	HL
	INC	DE
	DJNZ	SUBLP
	RET
;
.CMP	LD	A,(DE)
	CP	(HL)
	RET	NZ
	DEC	DE
	DEC	HL
	DJNZ	.CMP
	RET
;
.NEG	LD	A,(DE)
	CPL
	ADD	A,1
	LD	(DE),A
	DEC	B
	RET	Z
	INC	DE
	LD	C,0
NEGLP:	LD	A,(DE)
	CPL
	ADC	A,C
	LD	(DE),A
	INC	DE
	DJNZ	NEGLP
	RET
;
.SL1:	XOR	A
SL1LP:	RL	(HL)
	INC	HL
	DJNZ	SL1LP
	RET
;
.SR1:	XOR	A
SR1LP:	RR	(HL)
	DEC	HL
	DJNZ	SR1LP
	RET
;
.SL4:	XOR	A
SL4LP:	RLD
	INC	HL
	DJNZ	SL4LP
	RET
;
.SR4:	XOR	A
SR4LP	RRD
	DEC	HL
	DJNZ	SR4LP
	RET
;
.CLR:	LD	(DE),A
	INC	DE
	DJNZ	.CLR
	RET
;
.ZRC:	LD	A,(HL)
	AND	A
	RET	NZ
	INC	HL
	DJNZ	.ZRC
	RET
;
	END
