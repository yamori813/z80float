	TITLE	DTOJ (V02/L05)

	GLOBAL	DTOJ
	EXTERN	.CLR
	EXTERN	.ZRC
	EXTERN	.D10
	EXTERN	.SL4
	EXTERN	.M10
	EXTERN	.SR4
;
NBASE	EQU	30H
SPACE	EQU	20H
MINUS	EQU	2DH
DP	EQU	2EH
CD	EQU	44H
BYTE	EQU	8
DBYTEC	EQU	((BYTE-1)*8-3)*3/10
;
DTOJ	EQU	$
	LD	(SRCADR),HL
	LD	(DSTADR),DE
	XOR	A
	LD	(EXPD),A
	LD	DE,WRKT
	LD	HL,(SRCADR)
	LD	BC,BYTE-1
	LDIR
	LD	A,(HL)
	AND	7FH
	LD	(EXPS),A
	LD	A,(HL)
	AND	80H
	LD	(SGND),A
	XOR	A
	LD	(DE),A
	LD	HL,WRKT
	LD	B,BYTE-1
	CALL	.ZRC
	JR	NZ,DTOJ4
	LD	HL,EXPS
	LD	(HL),41H
DTOJ4:	LD	A,(EXPS)
	CP	41H
	JR	C,DTOJ1
	JR	Z,DTOJ2
DTOJ3:	LD	HL,WRKT
	LD	DE,WRKD
	LD	B,BYTE-1
	CALL	.D10
	LD	HL,EXPD
	INC	(HL)
	LD	A,(WRKT+BYTE-2)
	AND	0F0H
	JR	NZ,DTOJ3
	LD	HL,WRKT
	LD	B,BYTE-1
	CALL	.SL4
	LD	HL,EXPS
	DEC	(HL)
	JR	DTOJ4
DTOJ1:	LD	HL,WRKT
	LD	DE,WRKD
	LD	B,BYTE
	CALL	.M10
	LD	HL,EXPD
	DEC	(HL)
	LD	A,(WRKT+BYTE-1)
	AND	A
	JR	Z,DTOJ1
	LD	HL,WRKT+BYTE-1
	LD	B,BYTE
	CALL	.SR4
	LD	HL,EXPS
	INC	(HL)
	JR	DTOJ4
DTOJ2:	LD	A,(WRKT+BYTE-2)
	CP	0A0H
	JR	C,DTOJ5
	LD	HL,WRKT
	LD	DE,WRKD
	LD	B,BYTE-1
	CALL	.D10
	LD	HL,EXPD
	INC	(HL)
DTOJ5:	LD	HL,(DSTADR)
	LD	A,(SGND)
	AND	A
	JP	Z,DTOJ6
	LD	(HL),MINUS
DTOJ61:	INC	HL
	CALL	DJCNV
	LD	(HL),DP
	INC	HL
	LD	(DSTADR),HL
	LD	B,BYTE-1
DTOJL:	PUSH	BC
	LD	HL,WRKT
	LD	DE,WRKD
	LD	B,BYTE
	CALL	.M10
	LD	HL,(DSTADR)
	CALL	DJCNV
	LD	(DSTADR),HL
	POP	BC
	DJNZ	DTOJL
	LD	HL,(DSTADR)
	LD	(HL),CD
	INC	HL
	LD	A,(EXPD)
	BIT	7,A
	JR	NZ,DTOJ7
	LD	(HL),SPACE
DTOJ71:	LD	C,0
	LD	B,10
	INC	HL
DTOJ9:	SUB	B
	JR	C,DTOJ8
	INC	C
	JR	DTOJ9
DTOJ8:	ADD	A,10
	LD	B,A
	LD	A,C
	XOR	NBASE
	LD	(HL),A
	INC	HL
	LD	A,B
	XOR	NBASE
	LD	(HL),A
	INC	HL
	LD	(DSTADR),HL
	LD	HL,WRKT
	LD	DE,WRKD
	LD	B,BYTE
	CALL	.M10
	LD	A,(WRKT+BYTE-2)
	CP	50H
	JR	NC,DTOJC
DTOJR:	XOR	A
	LD	DE,(DSTADR)
	RET
DTOJ6:	LD	(HL),SPACE
	JP	DTOJ61
DTOJ7:	LD	(HL),MINUS
	NEG
	JR	DTOJ71
DTOJC:	LD	B,DBYTEC
	LD	HL,(DSTADR)
YY:	LD	A,(HL)
	CP	44H
	JR	Z,XX
	DEC	HL
	JR	YY
XX:	LD	B,DBYTEC
DTOJC1:	DEC	HL
DTOJC2:	LD	A,(HL)
	CP	DP
	JR	Z,DTOJC1
	INC	A
	LD	(HL),A
	CP	NBASE+10
	JR	C,DTOJR
	LD	(HL),NBASE
	DEC	HL
	DJNZ	DTOJC2
	LD	DE,(DSTADR)
	DEC	DE
	LD	HL,(DSTADR)
	DEC	HL
	DEC	HL
	LD	BC,DBYTEC
	LDDR
	LD	A,NBASE+1
	LD	(DE),A
	INC	DE
	LD	A,(DE)
	LD	C,A
	LD	A,DP
	LD	(DE),A
	LD	A,C
	INC	DE
	LD	(DE),A
	LD	HL,(DSTADR)
	INC	HL
	LD	A,(HL)
	CP	SPACE
	JR	Z,DTOJC3
	INC	HL
	INC	HL
	LD	A,(HL)
	DEC	A
	LD	(HL),A
	CP	NBASE
	JR	NC,DTOJR1
	LD	(HL),NBASE+9
	DEC	HL
	DEC	(HL)
	JP	DTOJR
DTOJC3:	INC	HL
	INC	HL
	LD	A,(HL)
	INC	A
	LD	(HL),A
	CP	NBASE+10
	JP	C,DTOJR
	LD	(HL),NBASE
	DEC	HL
	INC	(HL)
	JP	DTOJR
DTOJR1:	JP	NZ,DTOJR
	DEC	HL
	LD	A,(HL)
	CP	NBASE
	JP	NZ,DTOJR
	DEC	HL
	LD	(HL),SPACE
	JP	DTOJR
;
DJCNV:	EQU	$
	LD	DE,WRKT+BYTE-2
	LD	A,(DE)
	RRCA
	RRCA
	RRCA
	RRCA
	AND	0FH
	XOR	NBASE
	LD	(HL),A
	INC	HL
	LD	A,(DE)
	AND	0FH
	LD	(DE),A
	RET
;
WRKT:	DEFS	BYTE
WRKD:	DEFS	BYTE
SGND:	DEFB	0
EXPS:	DEFB	0
EXPD:	DEFB	0
SRCADR:	DEFW	0
DSTADR:	DEFW	0
;
	END
