	TITLE	ASUB2 (V02/03)
;
	GLOBAL	.M10
	GLOBAL	.D10
	GLOBAL	.STP
	GLOBAL	.DEC
	EXTERN	.SL1
	EXTERN	.ADD
	EXTERN	.CLR

CR	EQU	0DH
LF	EQU	0AH
COMMA	EQU	2CH
SPACE	EQU	20H
NBASE	EQU	30H
;
.M10	EQU	$
	LD	(DSTADR),HL
	LD	(WRKADR),DE
	LD	C,B
	LD	B,00
	LD	(BYTE),BC
	LDIR
	LD	HL,(WRKADR)
	LD	A,(BYTE)
	LD	B,A
	CALL	.SL1
	LD	HL,(WRKADR)
	LD	DE,(DSTADR)
	LD	BC,(BYTE)
	LDIR
	LD	HL,(WRKADR)
	LD	A,(BYTE)
	LD	B,A
	CALL	.SL1
	LD	HL,(WRKADR)
	LD	A,(BYTE)
	LD	B,A
	CALL	.SL1
	LD	HL,(WRKADR)
	LD	DE,(DSTADR)
	LD	A,(BYTE)
	LD	B,A
	CALL	.ADD
	RET
.D10	EQU	$
	LD	(DSTADR),HL
	LD	(WRKADR),DE
	LD	C,B
	LD	B,00
	LD	(BYTE),BC
	LDIR
	XOR	A
	LD	(DE),A
	LD	DE,(DSTADR)
	LD	A,(BYTE)
	LD	B,A
	INC	B
	XOR	A
	CALL	.CLR
	LD	A,(BYTE)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	B,A
	INC	B
D101:
	PUSH	BC
	LD	HL,(DSTADR)
	LD	A,(BYTE)
	LD	B,A
	CALL	.SL1
	LD	HL,(WRKADR)
	LD	BC,(BYTE)
	ADD	HL,BC
	LD	A,(HL)
	SUB	10
	JR	C,D102
	LD	(HL),A
	LD	HL,(DSTADR)
	SET	0,(HL)
D102:
	LD	HL,(WRKADR)
	LD	A,(BYTE)
	LD	B,A
	INC	B
	CALL	.SL1
	POP	BC
	DJNZ	D101
	LD	HL,(WRKADR)
	LD	BC,(BYTE)
	ADD	HL,BC
	LD	A,(HL)
	SRL	A
	RET
.STP:
	CP	CR
	RET	Z
	CP	LF
	RET	Z
	CP	COMMA
	RET	Z
	CP	SPACE
	RET
;
.DEC:
	CP	NBASE
	RET	C
	CP	NBASE+10
	CCF
	RET	C
	AND	0FH
	RET
;
DSTADR:	DEFW	0
WRKADR:	DEFW	0
BYTE:	DEFW	0
;
	END
