	TITLE	JTOD (V02/L05)
	GLOBAL	JTOD
	EXTERN	.CLR
	EXTERN	.DEC
	EXTERN	.M10
	EXTERN	.ADD
	EXTERN	.STP
	EXTERN	.ZRC
	EXTERN	.SL4
	EXTERN	.SR4
	EXTERN	.RND
	EXTERN	.D10
;
SPACE	EQU	20H
MINUS	EQU	2DH
PLUS	EQU	2BH
DP	EQU	2EH
CD	EQU	44H
CE	EQU	45H
BYTE	EQU	8
JBYTEC	EQU	((BYTE-1)*8-3)*3/10+1
SEIDO	EQU	40H OR (BYTE-1)*2
;
;	ASEG
;	ORG	300H
JTOD	EQU	$
	LD	(SRCADR),HL
	LD	(DSTADR),DE
	XOR	A
	LD	(SGND),A
	LD	(SGNS),A
	LD	(EXPS),A
	LD	DE,WRKT
	LD	B,BYTE+1
	CALL	.CLR
	LD	DE,WRKS
	LD	B,BYTE
	CALL	.CLR
	LD	HL,EXPD
	LD	(HL),SEIDO
JTOD1:	CALL	GETC
	CP	SPACE
	JR	Z,JTOD1
	CP	PLUS
	JR	Z,JTOD2
	CP	MINUS
	JR	Z,JTOD3
	CP	DP
	JR	Z,JTOD4
	CALL	.DEC
	JP	C,DERR
JTOD8:	LD	(WRKS),A
	LD	C,A
	LD	HL,SGNS
	LD	A,(HL)
	AND	A
	JR	NZ,JTOD5
	LD	A,C
	AND	A
	JR	Z,JTOD2
JTOD5:	INC	(HL)
	LD	HL,WRKT+1
	LD	DE,WRKD
	LD	B,BYTE-1
	CALL	.M10
	LD	HL,WRKS
	LD	DE,WRKT+1
	LD	B,BYTE
	CALL	.ADD
	LD	A,(SGNS)
	CP	JBYTEC
	JP	NC,JTOD7
JTOD2:	CALL	GETC
	CALL	.DEC
	JR	NC,JTOD8
JTOD72:	CP	DP
	JR	Z,JTOD4
JTOD45:	CP	CD
	JP	Z,JTOD9
	CP	CE
	JP	Z,JTOD9
	CALL	.STP
	JP	Z,JTOD10
	JP	DERR
JTOD3:	LD	HL,SGND
	LD	(HL),80H
	JR	JTOD2
JTOD4:	LD	A,(SGNS)
	CP	JBYTEC
	JR	NC,JTOD6
	CALL	GETC
	CALL	.DEC
	JR	C,JTOD45
	LD	(WRKS),A
	LD	C,A
	LD	HL,SGNS
	LD	A,(HL)
	AND	A
	JR	NZ,JTOD42
	LD	A,C
	AND	A
	JR	Z,JTOD43
JTOD42:	INC	(HL)
	LD	HL,WRKT+1
	LD	DE,WRKD
	LD	B,BYTE-1
	CALL	.M10
	LD	HL,WRKS
	LD	DE,WRKT+1
	LD	B,BYTE
	CALL	.ADD
JTOD43:	LD	HL,EXPS
	DEC	(HL)
	JR	JTOD4
JTOD6:	CALL	GETC
	CALL	.DEC
	JR	NC,JTOD6
	JP	JTOD45
JTOD7:	CALL	GETC
	CALL	.DEC
	JP	C,JTOD72
	LD	HL,EXPS
	INC	(HL)
	JR	JTOD7
JTOD9:	XOR	A
	LD	(WRKS),A
	LD	(WRKS+BYTE),A
	LD	(SGNS),A
	CALL	GETC
	CP	PLUS
	JR	Z,JTOD91
	CP	MINUS
	JP	Z,JTOD92
	CALL	.DEC
	JP	C,DERR
JTOD93:	LD	C,A
	LD	HL,SGNS
	LD	A,(HL)
	AND	A
	JR	NZ,JTOD94
	LD	A,C
	AND	A
	JR	Z,JTOD91
JTOD94:	INC	(HL)
	LD	HL,WRKS
	SLA	(HL)
	LD	A,(HL)
	SLA	(HL)
	SLA	(HL)
	ADD	A,(HL)
	ADD	A,C
	LD	(HL),A
	LD	A,(SGNS)
	CP	3
	JR	NC,JTOD95
JTOD91:	CALL	GETC
	CALL	.DEC
	JR	NC,JTOD93
	CALL	.STP
	JR	Z,JTOD11
	JP	DERR
JTOD95:	CALL	GETC
	CALL	.STP
	JP	NZ,DERR
JTOD11:	LD	HL,WRKS
	LD	A,(WRKS+BYTE)
	AND	A
	JR	Z,JTOD12
	LD	A,(HL)
	NEG
	LD	(HL),A
JTOD12:	LD	DE,EXPS
	LD	A,(DE)
	ADD	A,(HL)
	LD	(DE),A
	JP	PE,DOVF
JTOD10:	LD	HL,WRKT+1
	LD	B,BYTE-1
	CALL	.ZRC
	JP	Z,DUNDF
JTOD13:	LD	A,(WRKT+BYTE-1)
	AND	0F0H
	JR	NZ,JTOD14
	LD	HL,WRKT+1
	LD	B,BYTE-1
	CALL	.SL4
	LD	HL,EXPD
	DEC	(HL)
	JR	JTOD13
JTOD14:	XOR	A
	LD	(WRKT+BYTE),A
	LD	A,(EXPS)
	AND	A
	JP	M,JTOD15
	JR	Z,JTOD16
JTOD17:	LD	HL,WRKT+1
	LD	DE,WRKD
	LD	B,BYTE
	CALL	.M10
	LD	A,(WRKT+BYTE)
	AND	A
	JR	Z,JTOD18
	LD	HL,WRKT+BYTE
	LD	B,BYTE+1
	CALL	.SR4
	LD	HL,EXPD
	LD	A,(HL)
	ADD	A,1
	JP	M,DOVF
	LD	(HL),A
JTOD18:	LD	HL,EXPS
	DEC	(HL)
	JR	NZ,JTOD17
JTOD16:	LD	DE,WRKT
	LD	B,BYTE+1
	CALL	.RND
	LD	A,(WRKT+BYTE)
	AND	A
	JR	Z,JTOD19
	LD	HL,WRKT+BYTE
	LD	B,BYTE+1
	CALL	.SR4
	LD	HL,EXPD
	LD	A,(HL)
	ADD	A,1
	JP	M,DOVF
	LD	(HL),A
	JR	JTOD16
JTOD19:	LD	HL,WRKT+1
	LD	DE,(DSTADR)
	LD	BC,BYTE-1
	LDIR
	LD	A,(EXPD)
	LD	C,A
	LD	A,(SGND)
	XOR	C
	LD	(DE),A
	XOR	A
JTODR:	LD	HL,(SRCADR)
	RET
JTOD92:	LD	HL,WRKS+BYTE
	LD	(HL),80H
	JP	JTOD91
JTOD15:	LD	HL,WRKT
	LD	DE,WRKD
	LD	B,BYTE
	CALL	.D10
	LD	A,(WRKT+BYTE-1)
	AND	0F0H
	JR	NZ,JTOD20
	LD	HL,WRKT
	LD	B,BYTE
	CALL	.SL4
	LD	HL,EXPD
	LD	A,(HL)
	SUB	1
	JP	M,DUNDF
	LD	(HL),A
JTOD20:	LD	HL,EXPS
	INC	(HL)
	JR	NZ,JTOD15
	JP	JTOD16
DERR:	LD	A,1
	AND	A
	JR	JTODR
DOVF:	LD	A,81H
	AND	A
	JR	JTODR
DUNDF:	LD	DE,(DSTADR)
	LD	B,BYTE
	XOR	A
	CALL	.CLR
	JR	JTODR
;
GETC:	EQU	$
	LD	HL,(SRCADR)

	LD	A,(HL)
	INC	HL
	LD	(SRCADR),HL
	RET
;
WRKS:	DEFS	BYTE+1
WRKD:	DEFS	BYTE+1
WRKT:	DEFS	BYTE+1
EXPS:	DEFB	00
EXPD:	DEFB	00
SGNS:	DEFB	00
SGND:	DEFB	00
SRCADR:	DEFW	0000
DSTADR:	DEFW	0000
;
	END
