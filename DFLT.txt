	TITLE	DFLT (V05/L01)
;	GLOBAL	DADD
;	GLOBAL	DSUB
;	GLOBAL	DMLT
;	GLOBAL	DDIV
;	GLOBAL	DCMP
;	GLOBAL	DMOV
;	EXTERN	.ADD
;	EXTERN	.SUB
;	EXTERN	.CMP
;	EXTERN	.NEG
;	EXTERN	.SL1
;	EXTERN	.SR1
;	EXTERN	.SL4
;	EXTERN	.SR4
;	EXTERN	.CLR
;	EXTERN	.ZRC
;	EXTERN	.RND
;
BYTE	EQU	8
;
DADD:	CALL	FLD
	LD	A,(EXPS)
	LD	C,A
	LD	A,(EXPD)
	SUB	C
	JP	C,DADD1
	CP	BYTE*2-1
	JP	NC,DADD2
DADD12:	CALL	EXPEQ
DSUB1:	LD	A,(SGND)
	AND	A
	JR	Z,DADD3
	LD	DE,WRKD
	LD	B,BYTE*2-1
	CALL	.NEG
DADD3:	LD	A,(SGNS)
	AND	A
	JR	Z,DADD4
	LD	DE,WRKS
	LD	B,BYTE*2-1
	CALL	.NEG
DADD4:	LD	DE,WRKD
	LD	HL,WRKS
	LD	B,BYTE*2-1
	CALL	.ADD
	LD	A,(WRKD+(BYTE-1)*2)
	AND	80H
	LD	(SGND),A
	JR	Z,DADD5
	LD	DE,WRKD
	LD	B,BYTE*2-1
	CALL	.NEG
DADD5:	LD	A,(WRKD+(BYTE-1)*2)
	AND	A
	JR	Z,DADD6
	LD	HL,EXPD
	INC	(HL)
	LD	HL,WRKD+(BYTE-1)*2
	LD	B,BYTE+1
	CALL	.SR4
	LD	DE,WRKD+BYTE-2
	LD	B,BYTE
	CALL	.RND
	JR	DADD5
DADD6:	LD	HL,WRKD
	LD	B,BYTE*2-1
	CALL	.ZRC
	JR	Z,DADD7
DADD9:	LD	A,(WRKD+(BYTE-1)*2-1)
	AND	0F0H
	JR	NZ,DADD8
	LD	A,(EXPD)
	AND	A
	JR	Z,DADD7
	DEC	A
	LD	(EXPD),A
	LD	HL,WRKD
	LD	B,0+(BYTE-1)*2
	CALL	.SL4
	JR	DADD9
DADD8:	LD	DE,WRKD+BYTE-2
	LD	B,BYTE
	CALL	.RND
	LD	A,(WRKD+(BYTE-1)*2)
	AND	A
	JR	Z,DADD10
	LD	HL,EXPD
	INC	(HL)
	LD	HL,WRKD+(BYTE-1)*2
	LD	B,BYTE+1
	CALL	.SR1
	JR	DADD8
DADD10:	LD	A,(EXPD)
	AND	A
	JP	M,OVF
DADD11:	LD	DE,(DSTADR)
	LD	HL,WRKD+BYTE-1
	LD	BC,BYTE-1
	LDIR
	LD	A,(EXPD)
	LD	C,A
	LD	A,(SGND)
	XOR	C
	LD	(DE),A
DADD2:	XOR	A
	RET
DADD7:	XOR	A
	LD	(SGND),A
	LD	(EXPD),A
	LD	DE,WRKD+BYTE-1
	LD	B,BYTE-1
	CALL	.CLR
	JR	DADD11
DADD1:	NEG
	CP	BYTE*2-1
	JP	C,DADD12
	LD	HL,(SRCADR)
	LD	DE,(DSTADR)
	LD	BC,BYTE
	LDIR
	JR	DADD2
OVF:	LD	A,81H
	AND	A
	RET
;
DSUB:	CALL	FLD
	LD	A,(EXPS)
	LD	C,A
	LD	A,(EXPD)
	SUB	C
	JR	C,DSUB2
	CP	BYTE*2-1
	JP	NC,DADD2
DSUB3:	CALL	EXPEQ
	LD	DE,WRKS
	LD	B,BYTE*2-1
	CALL	.NEG
	JP	DSUB1
DSUB2:	NEG
	CP	BYTE*2-1
	JP	C,DSUB3
	LD	HL,(SRCADR)
	LD	DE,(DSTADR)
	LD	BC,BYTE
	LDIR
	DEC	D
	LD	A,(DE)
	XOR	80H
	LD	(DE),A
	JP	DADD2
;
DCMP:	CALL	FLD
	LD	A,(SGNS)
	LD	C,A
	LD	A,(SGND)
	XOR	C
	JR	NZ,DCMP1
	LD	A,(EXPS)
	LD	C,A
	LD	A,(EXPD)
	SUB	C
	RET	NZ
	LD	DE,WRKD+(BYTE-1)*2-1
	LD	HL,WRKS+(BYTE-1)*2-1
	LD	B,BYTE-1
	CALL	.CMP
	JR	C,DCMP4
	RET	Z
DCMP5:	LD	A,7FH
	OR	A
	RET
DCMP1:	LD	A,(SGND)
	SRL	A
	LD	B,A
	SRL	C
	LD	A,C
	SUB	B
	RET
SCMP2:	LD	A,(EXPD)
	LD	C,A
	LD	A,(EXPS)
	SUB	C
	RET	NZ
	LD	DE,WRKS+(BYTE-1)*2-1
	LD	HL,WRKD+(BYTE-1)*2-1
	LD	B,BYTE-1
	CALL	.CMP
	JR	C,DCMP4
	RET	Z
	JR	DCMP5
DCMP4:	LD	A,80H
	OR	A
	SCF
	RET
;
DMLT:	CALL	FLD
	LD	A,(SGNS)
	LD	HL,SGND
	XOR	(HL)
	LD	(HL),A
	LD	A,(EXPS)
	LD	HL,EXPD
	ADD	A,(HL)
	SUB	40H
	JP	C,DADD7
	JP	M,OVF
	LD	(HL),A
	LD	DE,WRKD
	LD	HL,WRKD+BYTE-1
	LD	BC,BYTE-1
	LDIR
	LD	B,BYTE-1
	XOR	A
	CALL	.CLR
	LD	B,0+(BYTE-1)*8*2
DMLTLP:	PUSH	BC
	LD	HL,WRKS+(BYTE-1)*2-1
	LD	B,BYTE-1
	CALL	.SR1
	JR	NC,DMLT1
	LD	DE,WRKT
	LD	HL,WRKD
	LD	B,0+(BYTE-1)*2
	CALL	.ADD
DMLT1:	LD	HL,WRKD
	LD	B,0+(BYTE-1)*2
; *** NUKE ***
	CALL	.SL1
	POP	BC
; *** NUKE ***
	DJNZ	DMLTLP
	LD	DE,WRKD
	LD	HL,WRKT
	LD	BC,0+(BYTE-1)*2
	LDIR
	JP	DADD6
;
DDIV:	CALL	FLD
	LD	A,(SGNS)
	LD	HL,SGND
	XOR	(HL)
	LD	(HL),A
	LD	A,(EXPS)
	LD	C,A
	LD	A,(EXPD)
	SUB	C
	ADD	A,40H
	JP	P,DDIV0
	JP	NC,OVF
	JP	DADD7
DDIV0:	LD	(EXPD),A
	LD	DE,WRKD+(BYTE-1)*2-1
	LD	HL,WRKS+(BYTE-1)*2-1
	LD	B,BYTE-1
	CALL	.CMP
	JR	C,DDIV1
	JR	Z,DDIV1
	LD	HL,EXPD
	INC	(HL)
	LD	HL,WRKD+(BYTE-1)*2-1
	LD	B,BYTE*2-2
	CALL	.SR4
DDIV1:	LD	B,0+(BYTE-1)*8*2+1
DDIVLP:	PUSH	BC
	LD	HL,WRKT
	LD	B,BYTE*2-1
	CALL	.SL1
	LD	DE,WRKD+(BYTE-1)*2
	LD	HL,WRKS+(BYTE-1)*2
	LD	B,BYTE*2-1
	CALL	.CMP
	JR	C,DDIV2
	LD	HL,WRKT
	SET	0,(HL)
	LD	DE,WRKD
	LD	HL,WRKS
	LD	B,BYTE*2-1
	CALL	.SUB
DDIV2:	LD	HL,WRKD
	LD	B,BYTE*2-1
	CALL	.SL1
	POP	BC
	DJNZ	DDIVLP
	LD	DE,WRKD
	LD	HL,WRKT
	LD	BC,BYTE*2-1
	LDIR
	JP	DADD5
DMOV:	LD	(SRCADR),HL
	LD	BC,BYTE
	LDIR
	DEC	HL
	LD	A,(HL)
	OR	A
	RET	NZ
	LD	HL,(SRCADR)
	LD	B,BYTE-1
	CALL	.ZRC
	RET	Z
	LD	A,7FH
	OR	A
	RET
;
FLD:	LD	(SRCADR),HL
	LD	(DSTADR),DE
	LD	DE,WRKT
	LD	B,0+(BYTE-1)*2
	XOR	A
	CALL	.CLR
	LD	DE,WRKD
	LD	B,BYTE-1
	CALL	.CLR
	LD	HL,(DSTADR)
	LD	BC,BYTE-1
	LDIR
	LD	A,(HL)
	AND	7FH
	LD	(EXPD),A
	LD	A,(HL)
	AND	80H
	LD	(SGND),A
	XOR	A
	LD	(DE),A
	LD	DE,WRKS
	LD	B,BYTE-1
	CALL	.CLR
	LD	HL,(SRCADR)
	LD	BC,BYTE-1
	LDIR
	LD	A,(HL)
	AND	7FH
	LD	(EXPS),A
	LD	A,(HL)
	AND	80H
	LD	(SGNS),A
	XOR	A
	LD	(DE),A
	RET
;
EXPEQ:	LD	A,(EXPS)
	LD	C,A
	LD	A,(EXPS)
	CP	C
	RET	Z
	JR	C,EXPEQ1
	INC	C
	LD	A,C
	LD	(EXPS),A
	LD	HL,WRKS+(BYTE-1)*2-1
	LD	B,0+(BYTE-1)*2
	CALL	.SR4
	JR	EXPEQ
EXPEQ1:	INC	A
	LD	(EXPD),A
	LD	HL,WRKD+(BYTE-1)*2
	LD	B,0+(BYTE-1)*2
	CALL	.SR4
;	JR	EXPEQ
;
WRKT:	DEFS	BYTE*2
WRKS:	DEFS	BYTE*2
WRKD:	DEFS	BYTE*2
SRCADR:	DEFW	0
DSTADR:	DEFW	0
SGNS:	DEFB	0
SGND:	DEFB	0
EXPS:	DEFB	0
EXPD:	DEFB	0
