	TITLE	DSQRT (V02/L01)
;
	GLOBAL	DSQRT
	EXTERN	DADD
	EXTERN	DSUB
	EXTERN	DCMP
	EXTERN	DMLT
	EXTERN	DDIV
	EXTERN	DMOV

; Delete not use macro

DSQRT	EQU	$
	XOR	A
	LD	(STATUS),A
	LD	(DSTADR),DE
	LD	DE,X
	CALL	DMOV
	LD	HL,X+7
	LD	A,(HL)
	LD	(HL),41H
	AND	A
	JP	M,DTERR
	JP	Z,DTZ
DSQRT6	EQU	$
	SUB	41H
	LD	C,A
	LD	HL,K+7
	SRA	A
	ADD	A,41H
	LD	(HL),A
	DEC	HL
	LD	A,C
	LD	C,10H
	AND	01H
	JR	Z,DSQRT1
	RLC	C
	RLC	C
DSQRT1	EQU	$
	LD	(HL),C
	LD	B,6
	XOR	A
DSQRT2	EQU	$
	DEC	HL
	LD	(HL),A
	DJNZ	DSQRT2
	LD	HL,A0
	LD	DE,XN
	CALL	DMOV
DSQRT3	EQU	$
	LD	HL,XN
	LD	DE,AN
	CALL	DMOV
	LD	HL,X
	LD	DE,XN
	CALL	DMOV
	LD	HL,AN
	LD	DE,XN
	CALL	DDIV
	LD	HL,AN
	LD	DE,XN
	CALL	DADD
	LD	HL,HALF
	LD	DE,XN
	CALL	DMLT
	LD	HL,AN
	LD	DE,XN
	CALL	DCMP
	JR	C,DSQRT3
	LD	HL,K
	LD	DE,XN
	CALL	DMLT
	LD	HL,XN
DSQRT8	EQU	$
	LD	BC,8
	LD	DE,(DSTADR)
	LDIR
	LD	A,(STATUS)
	AND	A
	RET
DTZ	EQU	$
	LD	HL,X
	LD	A,(HL)
	INC	HL
	LD	B,6
DSQRT4	EQU	$
	OR	(HL)
	INC	HL
	DJNZ	DSQRT4
	JR	Z,DSQRT5
	LD	A,(X+7)
	JP	DSQRT6
DSQRT5	EQU	$
	LD	HL,X
	XOR	A
	LD	(X+7),A
	JR	DSQRT8
DTERR	EQU	$
	LD	C,A
	LD	A,01H
	LD	(STATUS),A
	LD	A,C
	AND	7FH
	JP	DSQRT6
;
STATUS:	DEFB	0
DSTADR:	DEFW	0
X:	DEFB	0,0,0,0,0,0,0,0
XN:	DEFB	0,0,0,0,0,0,0,0
K:	DEFB	0,0,0,0,0,0,0,0
AN:	DEFB	0,0,0,0,0,0,0,0
HALF:	DEFB	0,0,0,0,0,0,80H,40H
A0:	DEFB	0,0,0,0,0,0,40H,41H
	END
